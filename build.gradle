plugins {
	id("fabric-loom") version "0.5.43" apply false
}

subprojects {
	apply plugin: "fabric-loom"

	repositories {
		mavenCentral()
		jcenter()

		maven {
			name = "Ladysnake Libs"
			url = "https://dl.bintray.com/ladysnake/libs"
		}

		maven {
			name = "TerraformersMC"
			url = "https://maven.terraformersmc.com/releases"
		}

		maven {
			name = "Shedaniel Maven"
			url = "https://maven.shedaniel.me"
		}
	}

	dependencies {
		//to change the versions see the gradle.properties file
		minecraft("com.mojang:minecraft:${project.minecraft_version}")
		mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
		modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	}

	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(8))
		}
	}

	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}

	jar {
		from rootProject.file("LICENSE")
	}

	loom {
		autoGenIDERuns = true;
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}
